plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "com.matthewprenger.cursegradle"
}

repositories {
    flatDir {
        dirs "libs"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

loom {
    forge {
        mixinConfigs = ["mixins.moreplanets.json", "mixins.moreplanets_forge.json"]

        dataGen {
            mod "moreplanets"
        }
    }

    launches {
        client {
            arg "--username", "${username}", "--uuid", "${uuid}"
        }
        data {
            arg "--existing", file("../common/src/main/resources/").absolutePath
        }
    }

    mixin {
        defaultRefmapName = "mixins.moreplanets_forge.refmap.json"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.mc_version}-${rootProject.forge_version}"
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }

    String lib = ":stevekung-lib-forge"

    if (!file("libs/stevekung-lib-forge.jar").exists()) {
        lib = "curse.maven:stevekungs-lib-296019:${rootProject.stevekung_lib_forge}"
        println("Use CurseForge artifact")
    } else {
        println("Use existing artifact")
    }
    modImplementation(lib)
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

String fileName = "${rootProject.archives_base_name}-mc${rootProject.mc_version}"

jar {
    classifier "dev"
    version "v${project.version}"
    baseName fileName
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
    version "v${project.version}"
    baseName fileName
    exclude "**/fabric/**/*.*"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    classifier "source"
    version "v${project.version}"
    baseName fileName
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "forge"
    version "v${project.version}"
    baseName fileName
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "397441"
            changelog = file("../CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "beta"
            addGameVersion "Forge"
            addGameVersion "Java 16"
            addGameVersion rootProject.mc_version

            mainArtifact(remapJar.archiveFile) {
                displayName = "[Forge] " + fileName + "-v${project.version}"
            }
            relations {
                requiredDependency "stevekungs-lib"
                requiredDependency "architectury-forge"
                optionalDependency "indicatia"
            }
            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
            addArtifact sourcesJar
            addArtifact shadowJar
        }
        options {
            forgeGradleIntegration = false
        }
    }
}

task moveDataGen(type: Exec) {
    doFirst {
        println "Start moving auto generated assets..."
        workingDir = file("M:/Modding/MorePlanets/")
        commandLine = ["cmd", "/C", "start", "clean_generated.bat"]
    }
}
runData.finalizedBy moveDataGen