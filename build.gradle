buildscript {
    repositories {
        jcenter()
        maven { url "https://files.minecraftforge.net/maven" }
        maven { url "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

def mcVersion = project.mc_version
def gcDevVersion = "1.12.2-${gc_require_version}"

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

version = project.version
group = "stevekung.mods.moreplanets"
archivesBaseName = "More-Planets-All"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mcp_mapping}"
    makeObfSourceJar = false

    replace "@FINGERPRINT@", project.sha1
    replace "4.0.1.-1", project.gc_require_version
    replaceIn "MorePlanetsMod.java"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    compile "mekanism:Mekanism-1.12.2:9.7.5.376:api"
    compile "com.github.glitchfiend.biomesoplenty:BiomesOPlenty:1.12.2-7.0.1.2441:api"
    compile "com.mod-buildcraft:buildcraft-api:7.99.24.1"
    compile "redstone-flux:RedstoneFlux-1.12:2.1.0.6:universal"
    compile "industrial-craft:industrialcraft-2-2.8.157:ex112:api"
    compile "player-api:PlayerAPI:1.12.1:1.0"
    compile "de.ellpeck.actuallyadditions:ActuallyAdditions:1.12.2-r144:api"
    compile "appeng:appliedenergistics2:rv6-stable-6:api"
    compile files(
		"libs/Galacticraft-API-" + gcDevVersion + ".jar",
		"libs/GalacticraftCore-Dev-" + gcDevVersion + ".jar",
		"libs/Galacticraft-Planets-Dev-" + gcDevVersion + ".jar",
		"libs/MicdoodleCore-Dev-" + gcDevVersion + ".jar"
	)
    deobfCompile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    deobfCompile "team.chisel.ctm:CTM:MC${ctm_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
    deobfCompile "stevekungs-lib:SteveKunG's-Lib-1.12.2:1.1.5:deobf"
    deobfCompile "indicatia:Indicatia-1.12.2:1.3.2:deobf"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    embed("org.spongepowered:mixin:0.7.11-SNAPSHOT") { transitive = false }
}

repositories {
    maven {
        url "http://tehnut.info/maven/"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://modmaven.k-4u.nl"
    }
    maven {
        url "http://chickenbones.net/maven/"
    }
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "http://maven.tterrag.com/"
    }
    maven {
        url "http://maven.thiakil.com/"
    }
    maven {
        url "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        url "https://mod-buildcraft.com/maven/"
    }
    maven {
        url "https://maven.chaosfield.at/"
    }
    maven {
        url "http://repo.spongepowered.org/maven/"
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.more_planets.refmap.json"
    }
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes("FMLAT": "moreplanets_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "MixinConfigs": "mixins.more_planets.json",
                   "ForceLoadAsMod": "true"
        )
    }
    baseName = "More-Planets-" + mcVersion
}

curseforge {
    apiKey = project.more_planets_api
    project {
        id = "261990"
        changelog = file("CHANGE_LOG.md")
        changelogType = "markdown"
        releaseType = "beta"
        addGameVersion mcVersion

        mainArtifact(jar) {
            displayName = "More-Planets-" + mcVersion + "-" + version
            relations {
                requiredLibrary "stevekungs-lib"
                optionalLibrary "jei"
                optionalLibrary "ctm"
                optionalLibrary "hwyla"
            }
        }
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = project.keystore_path
    alias = project.alias
    storePass = project.keystore_pass
    keyPass = project.keystore_pass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar